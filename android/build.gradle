group 'de.mintware.flutter_p2p'
version '1.0-SNAPSHOT'

buildscript {
  ext.kotlin_version = '1.8.0'

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.4.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.google.protobuf:protobuf-gradle-plugin:0.8.18"
  }
}

rootProject.allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
  compileSdkVersion 31

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    main.proto.srcDirs += '../protos'
  }
  defaultConfig {
    minSdkVersion 16
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  lintOptions {
    disable 'InvalidPackage'
  }
}

protobuf {
  protoc {
    artifact = 'com.google.protobuf:protoc:3.11.4'
  }
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option "lite"
        }
      }
    }
  }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1"
  implementation 'com.google.protobuf:protobuf-javalite:3.21.0'
  implementation 'androidx.appcompat:appcompat:1.6.1'
}
